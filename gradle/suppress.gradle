import groovy.util.slurpersupport.GPathResult
import groovy.xml.DOMBuilder
import groovy.xml.XmlUtil
import org.w3c.dom.Document
import org.w3c.dom.Element
import org.w3c.dom.Node
import org.w3c.dom.NodeList
import javax.xml.xpath.XPathConstants
import javax.xml.xpath.XPathExpression
import javax.xml.xpath.XPathFactory

void suppressCves(String reportName='dependency-check-report.xml') {
    // Default output directory is 'build/reports'
    def reportFile = new File(project.file(dependencyCheck.getOutputDirectory()), reportName)
    if (!reportFile.isFile()) {
        throw new FileNotFoundException("Report file not found.\n" +
                "\nPossible Solution: run :dependencyCheckAnalyze")
    }

    GPathResult report = new XmlSlurper().parse(new FileReader(reportFile))
    Set<String> unSuppressedCves = report.'**'
                                    .findAll { node -> node.name() == 'vulnerability' }
                                    .collect { it.name.text() } as Set<String>

    if (!unSuppressedCves.isEmpty()) {
        // Suppression file must be specified as 'suppressionFile=...' in the build script.
        File suppressionFile = project.file(dependencyCheck.getSuppressionFile())
        String suppressionXml = new String(suppressionFile.readBytes(), "UTF-8")
        Element suppressionDoc = DOMBuilder.parse(new StringReader(suppressionXml)).getDocumentElement()
        addNewSuppression(unSuppressedCves, suppressionDoc)
        suppressionFile.write(XmlUtil.serialize(cleanXml(suppressionDoc)))
    }
}

static void addNewSuppression(unSuppressedCves, suppressionDoc) {
    // Find 'Temporary Suppression' in note element and amend text content with new cve
    XPathExpression xpathExp = XPathFactory.newInstance().newXPath().compile(
            "//*[local-name()='notes']")
    def notesNodes =  xpathExp.evaluate(suppressionDoc, XPathConstants.NODESET)
    for (int i = 0; i < notesNodes.getLength(); i++) {
        Node noteNode = notesNodes.item(i)
        if (noteNode.getTextContent().contains('Temporary Suppression')) {
            unSuppressedCves.each {
                String whiteSpaceForIndent = "\n" + "\u0020".repeat(8) // white space for indentation
                Document document = suppressionDoc.getOwnerDocument()
                noteNode.appendChild(
                        document.createTextNode(whiteSpaceForIndent + it + " refer [Ticket]"))
                noteNode.getParentNode()
                        .appendChild(document.createElement("cve")).appendChild(document.createTextNode(it))
            }
        }
    }
}

static def cleanXml(Element suppression) {
    // Strip out all empty nodes
    XPathExpression xpathExp = XPathFactory.newInstance().newXPath().compile(
            "//text()[normalize-space(.) = '']")

    NodeList emptyTextNodes = xpathExp.evaluate(suppression, XPathConstants.NODESET) as NodeList
    if (emptyTextNodes.getLength()) {
        for (int i = 0; i < emptyTextNodes.getLength(); i++) {
            Node emptyTextNode = emptyTextNodes.item(i)
            emptyTextNode.getParentNode().removeChild(emptyTextNode)
        }
    }
    return suppression
}

task suppressCves() {
    doLast {
        // Optional argument to specify the name of the report file.
        // If no argument is specified, the default report file is 'dependency-check-report.xml'
        suppressCves()}
}
