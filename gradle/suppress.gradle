import groovy.util.slurpersupport.GPathResult
import groovy.xml.DOMBuilder
import groovy.xml.XmlUtil
import org.w3c.dom.Document
import org.w3c.dom.Element
import org.w3c.dom.Node
import org.w3c.dom.NodeList

import javax.xml.xpath.XPathConstants
import javax.xml.xpath.XPathExpression
import javax.xml.xpath.XPathFactory

void suppressCves(reportName) {
    reportName  = reportName == null || reportName as String == "" ? "dependency-check-report.xml" : reportName as String
    def reportFile = new File(project.file(dependencyCheck.getOutputDirectory()), reportName)
    if (!reportFile.isFile()) {
        throw new FileNotFoundException("Report file not found.\n" +
                "\nPossible Solution: run :dependencyCheckAnalyze")
    }

    GPathResult report = new XmlSlurper().parse(new FileReader(reportFile))
    Set<String> unSuppressedCves = report.'**'
                                    .findAll { node -> node.name() == 'vulnerability' }
                                    .collect { it.name.text() } as Set<String>

    if (!unSuppressedCves.isEmpty()) {
        File suppressionsFile = project.file(dependencyCheck.getSuppressionFile())
        def suppressionXml = new String(suppressionsFile.readBytes(), "UTF-8")
        Element suppressionsDoc = DOMBuilder.parse(new StringReader(suppressionXml)).getDocumentElement()
        addNewSuppressions(unSuppressedCves, suppressionsDoc)
        suppressionsFile.write(XmlUtil.serialize(cleanXml(suppressionsDoc)))
    }
}

static void addNewSuppressions(unSuppressedCves, suppressions) {
    unSuppressedCves.each {
        String cveName = it
        Document document = suppressions.getOwnerDocument()
        Element suppressTag = document.createElement("suppress")
        suppressTag.appendChild(document.createElement("notes")).appendChild(document.createTextNode("Ticket TBC"))
        suppressTag.appendChild(document.createElement("cve")).appendChild(document.createTextNode(cveName))
        suppressions.appendChild(suppressTag)
    }
}

static def cleanXml(Element suppressions) {
    // Strip out all empty nodes
    XPathExpression xpathExp = XPathFactory.newInstance().newXPath().compile(
            "//text()[normalize-space(.) = '']")

    NodeList emptyTextNodes = xpathExp.evaluate(suppressions, XPathConstants.NODESET) as NodeList
    if (emptyTextNodes.getLength()) {
        for (int i = 0; i < emptyTextNodes.getLength(); i++) {
            Node emptyTextNode = emptyTextNodes.item(i)
            emptyTextNode.getParentNode().removeChild(emptyTextNode)
        }
    }
    return suppressions
}

task suppressCves() {
    doLast {suppressCves("")}
}
